buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.0.3'
    }
}

plugins {
    id "nebula.os-package-base" version "2.0.3"
}
apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = "com.atlassian.jsm-integration"

project.ext.versions = new groovy.json.JsonSlurper().parseText(file("${projectDir}/version.json").text)
project.ext.versionSuffix = ''

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = "com.atlassian.jsm.integration"
    version = rootProject.version
    project.ext.versions = new groovy.json.JsonSlurper().parseText(file("${rootProject.projectDir}/version.json").text)

    if (project.ext.versions[name] != null) {
        version = project.ext.versions[name]
    }

    project.ext.versionSuffix = ''


    repositories {
        mavenCentral()
        maven {
            url "https://packages.atlassian.com/maven-3rdparty/"
        }
    }

    test.onlyIf {
        false
    }

    jar {
        manifest {
            attributes("Built-By": "http://www.atlassian.com",
                    "Specification-Title": project.name[0].toUpperCase() + project.name[1..-1],
                    "Specification-Product": project.name,
                    "Specification-Version": project.version,
                    "Specification-Vendor": "http://www.atlassian.com",
                    "Implementation-Title": project.name[0].toUpperCase() + project.name[1..-1],
                    "Implementation-Product": project.name,
                    "Implementation-Vendor": "http://www.atlassian.com",
                    "Implementation-Version": project.version,
            )
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.atlassian.com/maven-3rdparty/"
    }
}

configurations {
    netcool
    debian
}

dependencies {
    netcool 'com.sybase.jdbc:sybase:5.5.OG'
    debian 'org.vafer:jdeb:1.3'
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

//apply scripts
apply from: 'oem/oem.gradle'
apply from: 'opsview/opsview.gradle'
apply from: 'vcenter/vcenter.gradle'
apply from: 'vcsa/vcsa.gradle'
